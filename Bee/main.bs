use sys: io, text
use bee.run
pkg bee

def @alias
	names string get set
	.ctor names string
		.names = names

def program static
	main, args string[]
		if args.length > 1
			console.writeLine "Sorry, not supported now"
			return
		if args.length == 1
			var script string
			using var rd = new streamReader new fileStream args[0], fileMode.open, fileAccess.read
				script = rd.readToEnd()
			(new engine).eval script
			return

		console.writeLine "Bee Interactive Console"
		console.writeLine "Type 'return' or 'break' to exit"
		var cgen = new codeGenerator parser.opt.script, parser.opt.none
		var eng = new engine
		var sb = new stringBuilder
		for
			var more = sb.length > 0
			console.write more ? "... " : ">>> "
			var line = console.readLine()
			sb.appendLine line
			try
				cgen.unit sb.toString()
			catch exception
				if cgen.eof && line != ""; continue
				console.writeLine "Exception: " + exception.message
			var statement = cgen.codeAt > 0 && cgen.code[0] >= byte! opkind.statement
			if statement && line != "" && line != "return" && line != "break"
				continue
			sb.length = 0

			var code = cgen.toArray()
			cgen.reset()
			try
				eng.eval code
			catch exception
				console.writeLine "Exception: " + exception.message
				#if DEBUG
				console.writeLine exception.toString()
				#endif
				continue
			if eng.exit != opcode.undef
				console.writeLine "Exit code: {0}; Result: {1}", eng.exit, eng.result
				#if DEBUG
				console.read()
				#endif
				return
			console.writeLine eng.result.string