var targetAltitude = 80000000
var safeAltitude   = 72000
var ascentHeading  = 90

ship.throttle = 0
user.throttle = 1

var lastPitchReport = 0.0
def setPitch pitch
  ksp.flightControl.setRel ascentHeading, pitch
  if time() - lastPitchReport > 10
    lastPitchReport = time()
    print "Pitch: " + pitch

def checkStaging
  if !stage.ready
    return
  var nextDecoupler = ship.parts.nextDecoupler
  if nextDecoupler == null
    return
  if nextDecoupler.isType("LaunchClamp")
    print "Releasing launch clamps"
    stage
    return
  if !stage.engines.anyOperational
    print "No operational engine"
    stage
    return
  if stage.solidFuel + stage.liquidFuel <= 0.1
    print "Stage out of fuel"
    stage
    return

print "Target apoapsis: " + targetAltitude
print "Safe altitude:   " + safeAltitude
var lastApoAbove = 0.0
wait
until altitude >= safeAltitude and apoapsis >= targetAltitude 
  checkStaging()

  setPitch math.deg.acos math.clamp apoapsis / safeAltitude, 0, 1

  if apoapsis >= targetAltitude
    ship.throttle = 0
    if lastApoAbove == 0.0
      print "Apoapsis reached"
    lastApoAbove = time()
  else if lastApoAbove != 0 and time() - lastApoAbove < 10
    ship.throttle = 0
  else
    var power = 1
    if apoapsis > safeAltitude and altitude < safeAltitude
      power = math.max 0.3, (targetAltitude-apoapsis) / math.max(1.0, targetAltitude-safeAltitude)
    if apoapsis > targetAltitude * 0.95
      power = math.min power, 0.01+20*(1-apoapsis/targetAltitude)
    ship.throttle = math.min power, user.throttle

  wait

print "Safe altitude reached"
user.throttle = 0
ship.throttle = math.nan
ksp.FlightControl.disable
