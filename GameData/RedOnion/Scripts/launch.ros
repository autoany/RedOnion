var targetAltitude = 80000
var safeAltitude   = 72000
var ascentHeading  = 90
var firstAngle     = 10
var firstAltitude  = 1000
var narrowAltitude = 100

def gui
  var lblWidth = 120
  var boxWidth = 40
  var wnd = new ui.window
  def row label, value
    var row = wnd.add new ui.panel
    row.layout = ui.layout.horizontal
    var lbl = row.add new ui.label
    lbl.text = label
    lbl.minWidth = lblWidth
    var box = row.add new ui.textBox
    box.text = string value
    box.minWidth = boxWidth
    return box
  var ta = row "Target altitude: ", targetAltitude
  var sa = row "Safe altitude: ",   safeAltitude
  var ah = row "Ascent heading: ",  ascentHeading
  var fa = row "First angle: ",     firstAngle
  var a1 = row "First altitude: ",  firstAltitude
  var na = row "Narrow altitude: ", narrowAltitude
  var brow = wnd.add new ui.panel
  brow.layout = ui.layout.horizontal
  var cancel = brow.add new ui.button
  cancel.text = "Cancel"
  cancel.flexWidth = 1
  var launch = brow.add new ui.button
  launch.text = "Launch"
  launch.flexWidth = 1
  var selected = null
  cancel.click.add def => selected = false
  launch.click.add def => selected = true
  wnd.closed.add def => selected = false
  while selected == null
    wait
  if selected
    targetAltitude = double ta.text
    safeAltitude   = double sa.text
    ascentHeading  = double ah.text
    firstAngle     = double fa.text
    firstAltitude  = double a1.text
    narrowAltitude = double na.text
  wnd.dispose
  return selected
if !gui()
  return

ship.throttle = 0
user.throttle = 1

var lastPitchReport = 0.0
var prevPitchValue = 0
def setPitch pitch
  ksp.flightControl.setRel ascentHeading, pitch
  if time() - lastPitchReport > 1 and pitch != prevPitchValue
    lastPitchReport = time()
    prevPitchValue = pitch
    print "Pitch: " + pitch

def steer
  if altitude <= narrowAltitude
    setPitch 90
  else if altitude <= firstAltitude
    setPitch 90 - firstAngle * altitude/firstAltitude
  else setPitch (90 - firstAngle) * math.deg.acos(math.clamp
    ((apoapsis - firstAltitude) / (safeAltitude - firstAltitude)),
    0, 1) / 90
    
var lastApoAbove = 0.0
def throttle
  if apoapsis >= targetAltitude
    ship.throttle = 0
    if lastApoAbove == 0.0
      print "Apoapsis reached"
    lastApoAbove = time()
  else if lastApoAbove != 0 and time() - lastApoAbove < 10
    ship.throttle = 0
  else
    var power = 1
    if apoapsis > safeAltitude and altitude < safeAltitude
      power = math.max 0.3, (targetAltitude-apoapsis) / math.max(1.0, targetAltitude-safeAltitude)
    if apoapsis > targetAltitude * 0.95
      power = math.min power, 0.01+20*(1-apoapsis/targetAltitude)
    ship.throttle = math.min power, user.throttle

def staging
  if !stage.ready
    return
  var nextDecoupler = ship.parts.nextDecoupler
  if nextDecoupler == null
    return
  if nextDecoupler.isType("LaunchClamp")
    print "Releasing launch clamps"
    stage
    return
  if !stage.engines.anyOperational
    print "No operational engine"
    stage
    return
  if stage.solidFuel + stage.liquidFuel <= 0.1
    print "Stage out of fuel"
    stage
    return

var vd = [
    new vector.draw,
    new vector.draw
]
vd[0].scale = 10
vd[1].scale = 10
vd[1].color = color.blue
def vectors
  vd[0].vector = new vector ship.forward
  vd[1].vector = new vector ship.srfvel.normalized
  vd[0].show
  vd[1].show

var subs = [
  system.update(steer),
  system.update(throttle),
  system.update(vectors),
  system.idle(staging)
]

print "Target apoapsis: " + targetAltitude
print "Safe altitude:   " + safeAltitude
until altitude >= safeAltitude and apoapsis >= targetAltitude*.99
  wait

for var e in subs
  e.remove
for var d in vd
  d.hide

user.throttle = 0
ship.throttle = math.nan
ksp.FlightControl.disable

print "Safe altitude reached"
