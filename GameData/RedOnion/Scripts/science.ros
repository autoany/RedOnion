var done = false
var wnd = new ui.window false, "Science"
wnd.x = 160 - unity.screen.width/2
wnd.y = unity.screen.height/2 - 520
wnd.closed += def => done = true
var situation = wnd.addLabel()
var rowbox = wnd.add new ui.scrollbox
rowbox.width = 270
rowbox.height = 200

var rows = new list
var dict = new dictionary
var compact = false
var lastRefresh = time.never

def row_click button
  var row = button.tag
  if row.sci.perform(false)
    row.button.enabled = false
    lastRefresh = time.never

def refresh
//startup condition
  var body = science.body
  var sit = science.situation
  var biomeName = science.biomeName
//signal we are computing
  situation.text = "{0}; {1}; {2}".format body, sit, biomeName
//pre-clean
  for var row in rows
    row.present = false
    row.maxval = -1
//update
  for var sci in list ship.science // make a copy in case we get interrupted
    try
      if compact && !(sci.ready && sci.value >= 0.05)
        continue
      var has = science.contains(sci)
      if compact and has
        continue
      var row = dict[sci.experimentId]
      if row == null
        row = new object
        row.id = sci.experimentId
        row.sci = sci
        row.panel = rowbox.addHorizontal()
        row.panel.flexWidth = 1
        row.panel.width = 268
        row.label = row.panel.addLabel sci.experimentTitle
        row.label.width = 168
        row.value = row.panel.addLabel()
        row.value.width = 60
        row.button = row.panel.addButton sci.ready ? "DO" : "---", row_click
        row.button.width = 40
        row.button.tag = row
        row.present = true
        row.maxval = -1
        dict[row.id] = row
        rows.add row
      row.present = true
      row.label.fontStyle = has ? ui.fontStyle.italic : ui.fontStyle.normal
      var avail = sci.available
      var val = avail ? has ? sci.nextValue : sci.value : -0.1
      if val > row.maxval
        row.sci = sci
        row.maxval = val
        row.value.text = avail ? (sci.capacity >= 100 ?
          "{0:F1}/{1:F0}" : "{0:F1}/{1:F1}").format val, sci.capacity :
          sci.astate == "unavailable" ? "N/A" : sci.astate
        row.value.fontStyle = avail ? ui.fontStyle.normal : ui.fontStyle.italic
    catch var e
      print "Refresh {0}: {1}", sci.experimentId, e
  for var i = 0; i < rows.count
    var row = rows[i]
    if !row.present
      rows.removeAt i
      dict.remove row.id
      row.panel.dispose
    else
      row.button.enabled = row.maxval >= 0.0
      i++
//final output
  if science.body == body && sit == science.situation && biomeName == science.biomeName
    situation.text = "{0}, {1}, {2}".format body, sit, biomeName
    return true
  return false

var refreshing = false
def tryRefresh
  if refreshing
    return
  refreshing = true
  lastRefresh = time.now
  for var i = 0; i < 3; i++
    try
      if refresh()
        break
    catch var e
      print "Refresh: {0}", e
      var from = time.now
      while time.since(from) < 1
        wait
  refreshing = false

var btns = wnd.addHorizontal()
btns.flexWidth = 1
var xcomp = btns.addToggle "Compact", def
  compact = xcomp.pressed
  tryRefresh
btns.addHorizontal().flexWidth = 1
var all = btns.addButton "Gather All", def
  all.enabled = false
  need.enabled = false
  try
    for var row in rows
      if row.button.enabled
        row_click row.button
  catch var e
    print "Gather All: {0}", e
  finally
    all.enabled = true
    need.enabled = true
var need = btns.addButton "Gather >= 0.1", def
  all.enabled = false
  need.enabled = false
  try
    for var row in rows
      if row.button.enabled
        for var sci in row.sci
          var val = science.contains(sci) ? sci.nextValue : sci.value
          if val >= 0.01
            row_click row.button
            break
  catch var e
    print "Gather >= 0.1: {0}", e
  finally
    all.enabled = true
    need.enabled = true

try

  var subs = null // finally retains try-context but we want to call refresh first
  xcomp.pressed = true // calls tryRefresh
  subs = science.situationChanged tryRefresh
  wnd.show
  while !done
    wait
    if time.since(lastRefresh) >= 0.5
      tryRefresh

finally
  wnd.dispose
  if subs != null
    subs.remove
